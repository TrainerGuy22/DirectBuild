apply from: 'gradle/version.gradle'

apply plugin: 'groovy'
apply plugin: 'build-dashboard'
apply plugin: 'codenarc'
apply plugin: 'maven'
apply plugin: 'distribution'


defaultTasks 'jar'

group = "com.directmyfile"

ext {
    groovyVersion = '2.3.0-SNAPSHOT:indy'
    jdkHome = "${System.getProperty("java.home")}/../"
}

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.jfrog.org/oss-snapshot-local/'
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile('io.vertx:vertx-core:2.1M3') {
        exclude group: "com.hazelcast", module: "hazelcast"
    }
    compile 'io.vertx:lang-groovy:2.0.0-final'

    compile 'mysql:mysql-connector-java:5.1.28'
    compile 'com.directmyfile.jpower:jpower-core:1.3.1-SNAPSHOT'

    testCompile 'junit:junit:4.11'

    codenarc 'org.codenarc:CodeNarc:0.20'
}

check.dependsOn 'jar'

/* TODO: Port dbot.sh script to Gradle */
task makeDBot(type: Exec) {
    inputs.dir file("src/main/c")
    inputs.dir file("src/main/d")
    outputs.dir file("build/natives")
    commandLine "./gradle/dbot.sh", jdkHome
}

compileGroovy {
    groovyOptions.optimizationOptions.indy = true
}

tasks.withType(CodeNarc) {
    configFile = file("gradle/CodeNarc.groovy")
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

// Compile Java code with the Groovy compiler
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

sourceSets.main.resources.srcDirs += "build/binaries"

sourceCompatibility = 8
targetCompatibility = 8

jar {
    dependsOn 'makeDBot'

    manifest {
        attributes('Main-class': 'org.directcode.ci.core.Main')
    }

    // Pack Dependencies
    from configurations.compile.collect { it.directory ? it : zipTree(it) }

    // Pack Natives
    into("natives") {
        from fileTree("build/binaries/natives")
    }

    // Makes Jar Clean
    exclude "overviewj.html"
    exclude "overview.html"
    exclude "LICENSE.txt"

    // Fix Duplicate Zip Entry
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.0-20140322230012+0000"
}

apply from: 'gradle/pom.gradle'
apply from: 'gradle/upload.gradle'
apply from: 'gradle/dist.gradle'
