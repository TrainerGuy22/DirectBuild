apply plugin: 'groovy'
apply plugin: 'build-dashboard'
apply plugin: 'codenarc'
apply plugin: 'github-pages'

defaultTasks 'jar'

import org.ajoberstar.gradle.git.tasks.GitTag

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'org.ajoberstar:gradle-git:0.6.3' }
}

ext {
    groovyVersion = '2.3.0-SNAPSHOT:indy'
    jdkHome = "${System.getProperty("java.home")}/../"
    version = "0.9.0"
}

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.jfrog.org/oss-snapshot-local/'
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile('io.vertx:vertx-core:2.1M3') {
        exclude group: "com.hazelcast", module: "hazelcast"
    }
    compile 'io.vertx:lang-groovy:2.0.0-final'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'mysql:mysql-connector-java:5.1.28'
    testCompile 'junit:junit:4.11'

    codenarc 'org.codenarc:CodeNarc:0.20'
}

check.dependsOn 'jar'

task makeDBot(type: Exec) {
    inputs.dir file("src/main/c")
    inputs.dir file("src/main/d")
    outputs.dir file("build/natives")
    commandLine "./gradle/dbot.sh", jdkHome
}

task tagRelease(type: GitTag) {
    tagName = "v${version}"
    message = "Release of v${version}"
}

task release {
    dependsOn "jar", "publishGhPages", "tagRelease"
}

githubPages {
    repoUri = 'git@github.com:DirectMyFile/SimpleCI.git'
    pages {
        from(groovydoc.outputs.files) {
            into 'docs/groovydoc'
        }
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

tasks.withType(CodeNarc) {
    configFile = file("gradle/CodeNarc.xml")
}

// Compile Java code with the Groovy compiler
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

sourceCompatibility = 8
targetCompatibility = 8

jar {
    dependsOn 'makeDBot'

    manifest {
        attributes('Main-class': 'com.directmyfile.ci.core.Main')
    }

    // Pack Dependencies
    from configurations.compile.collect { it.directory ? it : zipTree(it) }

    // Pack Natives
    into("natives") {
        from fileTree("build/binaries/natives")
    }

    // Makes Jar Clean
    exclude "overviewj.html"
    exclude "overview.html"
    exclude "LICENSE.txt"

    // Fix Duplicate Zip Entry
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.11"
}
