apply plugin: 'groovy'
apply plugin: 'build-dashboard'
apply plugin: 'codenarc'
 
ext {
    groovyVersion = '2.2.0:indy'
    jdkHome = "${System.getProperty("java.home")}/../"
    version = "0.2.1"
}

repositories {
    mavenCentral()
 
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}
 
dependencies {
    compile "org.codehaus.groovy:groovy-all:${ext.groovyVersion}"
    compile ('io.vertx:vertx-core:2.0.2-final') {
        exclude group: "com.hazelcast", module: "hazelcast"
    }
    compile 'io.vertx:lang-groovy:2.0.0-final'
    compile 'org.mozilla:rhino:1.7R4'
    compile 'mysql:mysql-connector-java:5.1.26'
    testCompile 'junit:junit:4.11'

    codenarc 'org.codenarc:CodeNarc:0.19'

}

check.dependsOn 'jar'
 
task makeDBot(type: Exec) {
    commandLine "./gradle/dbot.sh", jdkHome
}
 
tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

tasks.withType(CodeNarc) {
    configFile = file("gradle/CodeNarc.xml")
}

// Compile java code with the groovy compiler
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]
 
jar {
    dependsOn 'makeDBot'

    manifest {
        attributes('Main-class': 'com.directmyfile.ci.Main')
    }
 
    // Pack Dependencies
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
 
    // Pack Natives
    into ("natives") {
        from fileTree("build/binaries/natives")
    }
    // Makes Jar Clean
    exclude "overviewj.html"
    exclude "overview.html"
    exclude "LICENSE.txt"
 
    // Fix Duplicate Zip Entry
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
 
task wrapper(type: Wrapper) {
  gradleVersion = "1.9-rc-4"
}
