apply plugin: 'groovy'
apply plugin: 'build-dashboard'
apply plugin: 'codenarc'
 
version = "0.2.0-SNAPSHOT"
 
ext {
    groovyVersion = '2.1.9:indy'
    jniInclude = "${System.getProperty("java.home")}/../include/"
}
 
sourceSets {
    main {
        output.resourcesDir = 'build/classes/production'
        output.classesDir = 'build/classes/production'
    }
}
 
repositories {
    mavenCentral()
 
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}
 
dependencies {
    compile "org.codehaus.groovy:groovy-all:${ext.groovyVersion}"
    compile ('io.vertx:vertx-core:2.0.2-final') {
        exclude group: "com.hazelcast", module: "hazelcast"
    }
    compile 'io.vertx:lang-groovy:2.0.0-final'
    compile 'org.mozilla:rhino:1.7R4'
    compile 'com.directmyfile:NanoBot:0.5.2-SNAPSHOT'
    compile 'mysql:mysql-connector-java:5.1.26'
    testCompile 'junit:junit:4.11'
}
 
task makeDBot(type: Exec) {
    commandLine './gradle/dbot.sh'
}
 
tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}
 
jar {
    dependsOn 'makeDBot'

    manifest {
        attributes('Main-class': 'com.directmyfile.ci.Main')
    }
 
    // Pack Dependencies
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
 
    // Pack Natives
    into ("natives") {
        from fileTree("build/binaries/natives")
    }
    // Makes Jar Clean
    exclude "TestNanoBot*.class"
    exclude "overviewj.html"
    exclude "overview.html"
    exclude "LICENSE.txt"
 
    // Fix Duplicate Zip Entry Bugs
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
 
task wrapper(type: Wrapper) {
  gradleVersion = "1.9-rc-2"
}