apply plugin: 'groovy'
apply plugin: 'build-dashboard'
apply plugin: 'codenarc'
apply plugin: 'maven'
apply plugin: 'distribution'
apply plugin: 'idea'

defaultTasks 'jar'

group = "com.directmyfile"

ext {
    groovyVersion = '2.3.0-SNAPSHOT:indy'
}

repositories {
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.jfrog.org/oss-snapshot-local/'
    }
}

dependencies {
    compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    compile('io.vertx:vertx-core:2.1RC1') {
        exclude group: "com.hazelcast", module: "hazelcast"
    }
    compile 'io.vertx:lang-groovy:2.1RC1'
    compile 'com.directmyfile.jpower:jpower-core:1.3.2-SNAPSHOT'

    testCompile 'junit:junit:4.11'

    codenarc 'org.codenarc:CodeNarc:0.20'
}

check.dependsOn 'jar'

compileGroovy {
    groovyOptions.optimizationOptions.indy = true
}

tasks.withType(CodeNarc) {
    configFile = file("gradle/CodeNarc.groovy")
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

// Compile Java code with the Groovy compiler
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

sourceCompatibility = 8
targetCompatibility = 8

jar {
    manifest {
        attributes('Main-class': 'org.directcode.ci.core.Main')
    }

    // Pack Dependencies
    from configurations.compile.findAll {
        it.name != "groovy-all-${groovyVersion - ":indy"}.jar"
    }.collect {
        it.directory ? it : zipTree(it)
    }

    // Makes Jar Clean
    exclude "overviewj.html"
    exclude "overview.html"
    exclude "LICENSE.txt"

    // Fix Duplicate Zip Entry
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.0-20140326084803+0000"
    distributionUrl = "http://services.gradle.org/distributions-snapshots/gradle-${gradleVersion}-all.zip"
}

apply from: 'gradle/dist.gradle'
